table Measure
	lineageTag: e487e955-5b22-4965-8217-99d45d76d72c

	measure 'Profit Margin' =
			
			DIVIDE(
			    [Profit],
			    [Revenue]
			)
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: Profit
		lineageTag: 9c9a1c82-865c-4ded-bf68-076f1aabfcb5

	measure ROAS =
			
			DIVIDE(
			    [Revenue],
			    [Expenses]
			)
		formatString: #,0.00\ "â‚¬";-#,0.00\ "â‚¬";#,0.00\ "â‚¬"
		displayFolder: ROAS
		lineageTag: 78691a73-5df1-4d6e-9850-c3c5d3098257

	measure ROI =
			
			DIVIDE(
			    [Profit],
			    [Expenses]
			)
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: ROI
		lineageTag: ac278d14-bbcd-49f3-941f-233ba057f15e

	measure Revenue = SUM(Metrics[Revenue])
		formatString: #,0.00\ "â‚¬";-#,0.00\ "â‚¬";#,0.00\ "â‚¬"
		displayFolder: Revenue
		lineageTag: 06d5d49c-8d1f-4541-8bf1-b6a5c3948de6

	measure Expenses = SUM(Metrics[Cost])
		formatString: #,0.00\ "â‚¬";-#,0.00\ "â‚¬";#,0.00\ "â‚¬"
		displayFolder: Expenses
		lineageTag: 1b72514e-7579-48f1-8fca-1a20672a22f6

	measure 'Dynamic Y_Max Combo Chart' = ```
			
			VAR SelectedMeasure = MIN('Performance Metrics'[SelectedProfitabilityMetric])
			VAR AdjustFactor =
			    IF(
			        SelectedMeasure = "Profit Margin" || SelectedMeasure = "ROI",
			        0.05,
			        0.5
			    )
			VAR SwitchMeasure =
			    SWITCH(
			        TRUE(),
			        SelectedMeasure = "ROAS",
			            MAXX(
			                                ALL('Calendar'[Month Year], 'Calendar'[Month Year S]),
			                                [ROAS]
			                            ),
			        SelectedMeasure = "ROI",
			            MAXX(
			                                ALL('Calendar'[Month Year], 'Calendar'[Month Year S]),
			                                [ROI]
			                            ),
			        SelectedMeasure = "Profit Margin",
			            MAXX(
			                                ALL('Calendar'[Month Year], 'Calendar'[Month Year S]),
			                                [Profit Margin]
			                            ),
			        SelectedMeasure = "Revenue",
			            MAXX(
			                                ALL('Calendar'[Month Year], 'Calendar'[Month Year S]),
			                                [Revenue]
			                            ),
			        SelectedMeasure = "Expenses",
			            MAXX(
			                                ALL('Calendar'[Month Year], 'Calendar'[Month Year S]),
			                                [Expenses]
			                            )
			
			    )
			VAR MaxTarget =
			    MAXX(
			        ALL('Calendar'[Month Year], 'Calendar'[Month Year S]),
			        [Dynamic Target]
			    )
			VAR MaxValue = 
			    ROUNDUP(
			        MAX(SwitchMeasure, MaxTarget),
			        1
			    ) + AdjustFactor
			RETURN MaxValue
			
			```
		lineageTag: a0365081-a8d5-4e9a-b98c-a1f80b476a47

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Dynamic Profit Tooltip' = ```
			
			    IF(
			        --SELECTEDVALUE(SelectedProfitabilityMetric[SelectedProfitabilityMetric Fields]) = "'Measure'[Profit Margin]",
			        MIN('Performance Metrics'[SelectedProfitabilityMetric]) = "Profit Margin",
			        [Profit],
			        BLANK()
			    )
			```
		formatString: #,0.00\ "â‚¬";-#,0.00\ "â‚¬";#,0.00\ "â‚¬"
		displayFolder: Profit
		lineageTag: 43a2de06-e686-44c0-895f-068babd888a3

	measure 'Dynamic Detail Label' = ```
			
			--VAR SelectedMeasure = SELECTEDVALUE(SelectedProfitabilityMetric[SelectedProfitabilityMetric Fields])
			VAR SelectedMeasure = MIN('Performance Metrics'[SelectedProfitabilityMetric])
			VAR ArrowUp = UNICHAR(9650) -- arrow up
			VAR ArrowDown = UNICHAR(9660) -- arrow down
			VAR CurrMonthMetric =
			    SWITCH(
			        TRUE(),
			        SelectedMeasure = "ROAS", [ROAS],
			        SelectedMeasure = "ROI", [ROI],
			        SelectedMeasure = "Profit Margin", [Profit Margin]
			    )
			VAR PrevMonthMetric =
			    SWITCH(
			        TRUE(),
			        SelectedMeasure = "ROAS",
			            CALCULATE(
			                [ROAS],
			                DATEADD(
			                    'Calendar'[Date],
			                    -1,
			                    MONTH
			                )
			            ),
			        SelectedMeasure = "ROI",
			        CALCULATE(
			            [ROI],
			            DATEADD(
			                'Calendar'[Date],
			                -1,
			                MONTH
			            )
			        ),
			    SelectedMeasure = "Profit Margin",
			        CALCULATE(
			            [Profit Margin],
			            DATEADD(
			                'Calendar'[Date],
			                -1,
			                MONTH
			            )
			        )
			    )
			VAR SwitchArrow =
			    IF(
			        CurrMonthMetric > PrevMonthMetric,
			        ArrowUp,
			        ArrowDown
			    )
			VAR Delta =
			    IF(
			        SelectedMeasure = "ROAS",
			        --CurrMonthMetric - PrevMonthMetric,
			        [Dynamic Difference],
			        100*(CurrMonthMetric - PrevMonthMetric)
			    )
			VAR FormatLabel = 
			    IF(
			        SelectedMeasure = "ROAS",
			        "+0.00 â‚¬;-0.00 â‚¬",
			        "+0.00;-0.00"
			    )
			VAR Label =
			    IF(
			        ISBLANK(PrevMonthMetric),
			        BLANK(),
			        SwitchArrow & " " & FORMAT(
			                                Delta,
			                                FormatLabel
			                            )
			    )
			RETURN
			    Label
			```
		lineageTag: 77c06c77-52ce-4980-924f-4e7385e681bc

	measure 'Dynamic Difference' = ```
			-- use this measure to set dynamic color green/red for the detail label
			--VAR SelectedMeasure = SELECTEDVALUE(SelectedProfitabilityMetric[SelectedProfitabilityMetric Fields])
			VAR SelectedMeasure = MIN('Performance Metrics'[SelectedProfitabilityMetric])
			VAR CurrMonthMetric =
			    SWITCH(
			        TRUE(),
			        SelectedMeasure = "ROAS", [ROAS],
			        SelectedMeasure = "ROI", [ROI],
			        SelectedMeasure = "Profit Margin", [Profit Margin]
			    )
			VAR PrevMonthMetric =
			    SWITCH(
			        TRUE(),
			        SelectedMeasure = "ROAS",
			            CALCULATE(
			                [ROAS],
			                DATEADD(
			                    'Calendar'[Date],
			                    -1,
			                    MONTH
			                )
			            ),
			        SelectedMeasure = "ROI",
			        CALCULATE(
			            [ROI],
			            DATEADD(
			                'Calendar'[Date],
			                -1,
			                MONTH
			            )
			        ),
			    SelectedMeasure = "Profit Margin",
			        CALCULATE(
			            [Profit Margin],
			            DATEADD(
			                'Calendar'[Date],
			                -1,
			                MONTH
			            )
			        )
			    )
			VAR MetricDifference = 
			    100*(CurrMonthMetric -  PrevMonthMetric)
			RETURN
			    MetricDifference
			```
		lineageTag: 3925b5d0-afae-4a94-8b0b-30a302b2b473

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Label Period' =
			
			SELECTEDVALUE('Calendar'[Month Year])
		lineageTag: fbe745d4-f7a8-4865-a21d-8d8d33c5cc65

	measure 'ROAS Target' =
			
			VAR SelectedMonth = MIN('Calendar'[Month Year S])
			VAR Cost_ =
			    CALCULATE(
			        [Expenses],
			        ALL('Calendar'[Month Year], 'Calendar'[Month Year S]),
			        'Calendar'[Month Year S] < SelectedMonth
			    )
			VAR Revenue_ =
			    CALCULATE(
			        [Revenue],
			        ALL('Calendar'[Month Year], 'Calendar'[Month Year S]),
			        'Calendar'[Month Year S] < SelectedMonth
			    )
			VAR ROAS_Target =
			IF(
			    ISBLANK(Cost_),
			    2.7,
			    DIVIDE(
			        Revenue_,
			        Cost_
			    )
			)
			RETURN ROAS_Target
		formatString: #,0.00\ "â‚¬";-#,0.00\ "â‚¬";#,0.00\ "â‚¬"
		displayFolder: ROAS
		lineageTag: f47f001c-8eac-4426-9ca6-93f4e68506b3

	measure 'ROI Max Value' =
			
			ROUNDUP(
			    [ROI],
			    0) + 1
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: ROI
		lineageTag: beeebb47-82ba-4386-bdf7-ac7f8d2bebd9

	measure 'ROI Target Visual' =
			
			VAR TargetReached = "âœ”"
			VAR TargetMissed = "ðŸš©"
			VAR SwitchLabel =
			    IF(
			        [ROI] >= [ROI Target],
			        TargetReached,
			        TargetMissed
			    )
			RETURN
			    SwitchLabel
		displayFolder: ROI
		lineageTag: db68acc1-6091-4f89-b71e-0538b12995b2

	measure 'Profit Margin Target Visual' =
			
			VAR TargetReached = "âœ”"
			// VAR TargetMissed = "âœ–"
			--VAR TargetReached = "âœ”ï¸"
			VAR TargetMissed = "ðŸš©"
			VAR SwitchLabel =
			    IF(
			        [Profit Margin] >= [Profit Margin Target],
			        TargetReached,
			        TargetMissed
			    )
			-- VAR Delta = ROUND([Profit] - [Profit Margin Target],2)
			RETURN
			    SwitchLabel
		displayFolder: Profit
		lineageTag: 1bb0fc19-fa0f-477a-a286-3309d8b6c911

	measure 'ROAS Target Visual' =
			
			VAR TargetReached = "âœ”"
			VAR TargetMissed = "ðŸš©"
			VAR SwitchLabel =
			    IF(
			        [ROAS] >= [ROAS Target],
			        TargetReached,
			        TargetMissed
			    )
			RETURN
			    SwitchLabel
		displayFolder: ROAS
		lineageTag: a79ab7ad-1e1b-494c-9f9d-d00330508f7a

	measure 'Dynamic Format Color' =
			
			--VAR SelectedMeasure = SELECTEDVALUE(SelectedProfitabilityMetric[SelectedProfitabilityMetric Fields])
			VAR SelectedMeasure = MIN('Performance Metrics'[SelectedProfitabilityMetric])
			VAR SwitchMeasure =
			    SWITCH(
			        TRUE(),
			        SelectedMeasure = "Profit Margin", [Profit Margin],
			        SelectedMeasure = "ROAS", [ROAS],
			        SelectedMeasure = "ROI", [ROI],
			        SelectedMeasure = "Revenue", [Revenue],
			        [Expenses]
			    )
			VAR SetColorIndex =
			    IF(
			        SwitchMeasure >= [Dynamic Target],
			        1,
			        0
			    )
			RETURN SetColorIndex
		formatString: 0
		lineageTag: a1e75aef-fe4f-42bb-b440-3b7e85f0a928

	measure 'Dynamic Delta vs Target' = ```
			
			VAR TargetReached = UNICHAR(9989)
			VAR TargetMissed = UNICHAR(10060)
			--VAR SelectedMeasure = SELECTEDVALUE(SelectedProfitabilityMetric[SelectedProfitabilityMetric Fields])        
			VAR SelectedMeasure = MIN('Performance Metrics'[SelectedProfitabilityMetric])
			VAR SwitchMeasure =
			    SWITCH(
			        TRUE(),
			        SelectedMeasure = "Profit Margin", [Profit Margin],
			        SelectedMeasure = "ROAS", [ROAS],
			        [ROI]
			    )
			VAR SwitchIcon =
			    IF(
			        SwitchMeasure >= [Old_Dynamic Target],
			        TargetReached,
			        TargetMissed
			    )
			VAR DeltaDifference = SwitchMeasure - [Old_Dynamic Target]
			VAR FormatResult = 
			        SwitchIcon & FORMAT(
			                        DeltaDifference,
			                        "+0.00;-0.00"
			        )
			RETURN FormatResult
			
			
			```
		lineageTag: a8043ac7-dad1-403c-b187-e21610e56660

	measure 'Dynamic Target Format' = ```
			
			--VAR SelectedMeasure = SELECTEDVALUE(SelectedProfitabilityMetric[SelectedProfitabilityMetric Fields])
			VAR SelectedMeasure = MIN('Performance Metrics'[SelectedProfitabilityMetric])
			// VAR Target =
			//     IF(
			//         SelectedMeasure = "'Measure'[ROAS]",
			//         [Dynamic Target],
			//         100*[Dynamic Target]
			//     )
			VAR SwitchMeasure =
			    // FORMAT(
			    //     [Dynamic Target],
			    //     IF(
			    //         SelectedMeasure = "ROAS" || SelectedMeasure = "Revenue" || SelectedMeasure = "Expenses",
			    //         "0 â‚¬",
			    //         "0%"
			    //     )
			    // ) 
			    FORMAT(
			        [Dynamic Target],
			        SWITCH(
			            TRUE(),
			            SelectedMeasure = "ROAS" || SelectedMeasure = "Revenue" || SelectedMeasure = "Expenses", "#,## â‚¬",
			            "0%"
			        )
			    ) 
			
			RETURN SwitchMeasure
			
			```
		lineageTag: b6810fee-28a9-4cb3-b4ea-2819e39c58d7

	measure 'Title Combo Chart' =
			
			VAR SelectedMetric = MIN('Performance Metrics'[SelectedProfitabilityMetric])
			VAR Label2 = " over Time"
			VAR ConcatenateLabel = SelectedMetric & " by Month"
			RETURN ConcatenateLabel
		lineageTag: b40fffa1-6564-4c84-bb6e-18cd1cb489e1

	measure 'Profit Margin Max Value' =
			
			ROUNDUP(
			    [Profit Margin],
			    0) + 0.5
		displayFolder: Profit
		lineageTag: ec5c91ee-6867-4397-9f11-d86c1d20ab65

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'ROAS Max Value' =
			
			ROUNDUP(
			    [ROAS],
			    1) + 2
		displayFolder: ROAS
		lineageTag: 9b38765b-2258-47e2-af66-d0c0955eb840

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Profit Margin Format Color' =
			
			IF(
			    [Profit Margin] >= [Profit Margin Target],
			    1,
			    0
			)
		formatString: 0
		displayFolder: Profit
		lineageTag: d596f25f-2527-4fd7-8900-c77977ac45fd

	measure 'ROI Format Color' =
			
			IF(
			    [ROI] >= [ROI Target],
			    1,
			    0
			)
		formatString: 0
		displayFolder: ROI
		lineageTag: 809e66aa-3e54-4d9f-9c72-6baa97881a1b

	measure 'ROAS Format Color' =
			
			IF(
			    [ROAS] >= [ROAS Target],
			    1,
			    0
			)
		formatString: 0
		displayFolder: ROAS
		lineageTag: d07f7692-51ed-42d6-82f8-79f64bb701bc

	measure 'Old_Profit Margin Target' = ```
			
			VAR NegativeReplacer = 0.15
			VAR FirstPeriod = 
			    CALCULATE(
			        MIN('Calendar'[Month Year S]),
			        ALL('Calendar'[Month Year],'Calendar'[Month Year S])
			    )
			VAR LatestSelectedMonth = MAX('Calendar'[Month Year S])
			VAR Target =
			    IF(
			        MIN('Calendar'[Month Year S]) = FirstPeriod,
			        BLANK(),
			        AVERAGEX(
			            FILTER(
			                ALL('Calendar'[Month Year],'Calendar'[Month Year S]),
			                'Calendar'[Month Year S] <= LatestSelectedMonth
			            ),
			            [Profit Margin]
			        )
			    )
			VAR ReplaceNegativeValues = 
			    IF(
			        Target < 0, 
			        NegativeReplacer, 
			        Target
			    )
			RETURN ReplaceNegativeValues
			
			
			
			```
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: Profit
		lineageTag: 4db18980-e97d-4a47-b3cf-36237c0a12f8

	measure 'Profit Margin Target' =
			
			VAR Selected_Month = MIN('Calendar'[Month Year S])
			VAR Revenue_ =
			    CALCULATE(
			        [Revenue],
			        ALL('Calendar'[Month Year], 'Calendar'[Month Year S]),
			        'Calendar'[Month Year S] < Selected_Month
			    )
			VAR Profit_ =
			    CALCULATE(
			        [Profit],
			        ALL('Calendar'[Month Year], 'Calendar'[Month Year S]),
			        'Calendar'[Month Year S] < Selected_Month
			    )
			VAR ProfitMargin_ =
			    IF(
			        ISBLANK(Revenue_),
			        0.50,
			        DIVIDE(
			            Profit_,
			            Revenue_
			        )
			    )
			RETURN ProfitMargin_
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: Profit
		lineageTag: 5f1fba6d-c918-4d02-a2c0-d492f1c4efee

	measure Profit = [Revenue] - [Expenses]
		formatString: "Â£"#,0.###############;-"Â£"#,0.###############;"Â£"#,0.###############
		displayFolder: Profit
		lineageTag: 10269047-3062-415b-aa48-40ab7452f0ab

		annotation PBI_FormatHint = {"currencyCulture":"en-GB"}

	measure 'ROI Target' =
			
			VAR SelectedMonth = MIN('Calendar'[Month Year S])
			VAR Cost_ =
			    CALCULATE(
			        [Expenses],
			        ALL('Calendar'[Month Year], 'Calendar'[Month Year S]),
			        'Calendar'[Month Year S] < SelectedMonth
			    )
			VAR ProfitM =
			    CALCULATE(
			        [Profit],
			        ALL('Calendar'[Month Year], 'Calendar'[Month Year S]),
			        'Calendar'[Month Year S] < SelectedMonth
			    )
			VAR ROAS_Target =
			    IF(
			        ISBLANK(Cost_),
			        1.60,
			        DIVIDE(
			            ProfitM,
			            Cost_
			        )
			    )
			RETURN ROAS_Target
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: ROI
		lineageTag: 4f078a17-6c24-4b90-a7a1-ade41d7c9b25

	measure 'Dynamic Target' =
			
			VAR SelectedMeasure = MIN('Performance Metrics'[SelectedProfitabilityMetric])
			VAR SwitchTarget =
			    SWITCH(
			        TRUE(),
			        SelectedMeasure = "Profit Margin", [Profit Margin Target],
			        SelectedMeasure = "ROAS", [ROAS Target],
			        SelectedMeasure = "ROI", [ROI Target],
			        SelectedMeasure = "Revenue", [Revenue Target],
			        SelectedMeasure = "Expenses", [Expenses Target]
			    )
			RETURN SwitchTarget
		lineageTag: 54c8e762-5226-465b-b336-0190a8e66ec5

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'VAR SwitchTarget' = ```
			
			VAR SelectedMeasure = MIN('Performance Metrics'[SelectedProfitabilityMetric])
			VAR SwitchMeasure =
			    SWITCH(
			        TRUE(),
			        SelectedMeasure = "Profit Margin",
			            MAXX(
			                ALL('Calendar'[Month Year], 'Calendar'[Month Year S]),
			                [Profit Margin]
			            ),
			        SelectedMeasure = "ROAS",
			            MAXX(
			                ALL('Calendar'[Month Year], 'Calendar'[Month Year S]),
			                [ROAS]
			            ),
			        SelectedMeasure = "ROI",
			            MAXX(
			                ALL('Calendar'[Month Year], 'Calendar'[Month Year S]),
			                [ROI]
			            )
			    )
			VAR MaxValue =
			    MAX(
			        SwitchMeasure,
			        [Dynamic Target]
			    )
			RETURN
			    SwitchMeasure
			
			```
		lineageTag: 5d241612-4a8a-47ae-ad87-302c8746b4ca

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Dynamic Target Visual' =
			
			VAR SelectedMetric = MIN('Performance Metrics'[SelectedProfitabilityMetric])
			VAR SwitchPerformance =
			    SWITCH(
			        TRUE(),
			        SelectedMetric = "ROAS", [ROAS Target Visual],
			        SelectedMetric = "ROI", [ROI Target Visual],
			        SelectedMetric = "Profit Margin", [Profit Margin Target Visual],
			        SelectedMetric = "Revenue", [Revenue Target Visual],
			        SelectedMetric = "Expenses", [Expenses Target Visual]
			    )
			RETURN SwitchPerformance
		lineageTag: d83a35c7-cc92-4717-b17c-a860b975af92

	measure 'Profit Margin PM' = ```
			
			VAR Delta = [Profit Margin] - [Profit Margin Target]
			VAR FormatDelta = FORMAT([Profit Margin] - [Profit Margin Target], "+0.00;-0.00") 
			VAR Label =
			    IF(
			        Delta >= 0, " over expected",
			        " below expected"
			    )
			RETURN FormatDelta & Label
			```
		displayFolder: Profit
		lineageTag: 57d30e08-c007-4d47-941f-09aec009f00c

	measure 'Custom Title Dimension' = ```
			
			VAR SelectedColumn = 
			    IF(
			        MIN('Selected Dimension'[Selected Dimension]) = "Campaign", "Campaigns",
			        "Platforms"
			        )
			VAR Label = "- Top 5 " & SelectedColumn
			RETURN Label
			```
		lineageTag: 01420a77-44d5-42c2-b910-569eb96278d1

	measure 'Revenue Target' = ```
			
			VAR SelectedMonth = MIN('Calendar'[Month Year S])
			VAR RevenueTarget =
			    // CALCULATE(
			    //     [Expenses],
			    //     ALL('Calendar'[Month Year], 'Calendar'[Month Year S]),
			    //     'Calendar'[Month Year S] < SelectedMonth
			    // )
			AVERAGEX(
			    FILTER(
			        ALL('Calendar'[Month Year], 'Calendar'[Month Year S]),
			        'Calendar'[Month Year S] < SelectedMonth
			    ),
			    [Revenue]
			)
			RETURN RevenueTarget
			
			```
		formatString: #,0.00\ "â‚¬";-#,0.00\ "â‚¬";#,0.00\ "â‚¬"
		displayFolder: Revenue
		lineageTag: 3ed9e3d7-529f-4e13-abde-b67a591bd18e

	measure 'Expenses Target' =
			
			VAR SelectedMonth = MIN('Calendar'[Month Year S])
			VAR ExpensesTarget =
			    AVERAGEX(
			        FILTER(
			            ALL('Calendar'[Month Year], 'Calendar'[Month Year S]),
			            'Calendar'[Month Year S] < SelectedMonth
			        ),
			        [Expenses]
			    )
			RETURN ExpensesTarget
		formatString: #,0.00\ "â‚¬";-#,0.00\ "â‚¬";#,0.00\ "â‚¬"
		displayFolder: Expenses
		lineageTag: 10f4b1c4-e758-4f6c-b6d2-d79587d15473

	measure 'Revenue Target Visual' =
			
			VAR TargetReached = "Target achieved " & UNICHAR(9989)
			VAR TargetMissed = "Below expectation " & UNICHAR(10060)
			VAR SwitchLabel =
			    IF(
			        [Revenue] > [Revenue Target],
			        TargetReached,
			        TargetMissed
			    )
			RETURN
			    SwitchLabel
		displayFolder: Revenue
		lineageTag: 21aacbb8-9f0c-49af-8a5f-b51d40275456

	measure 'Expenses Target Visual' =
			
			VAR TargetReached = "Target achieved " & UNICHAR(9989)
			VAR TargetMissed = "Below expectation " & UNICHAR(10060)
			VAR SwitchLabel =
			    IF(
			        [Expenses] > [Expenses Target],
			        TargetReached,
			        TargetMissed
			    )
			RETURN
			    SwitchLabel
		displayFolder: Expenses
		lineageTag: d1207e84-efae-4e6d-8323-4a768fef344b

	measure 'Target Output Label' =
			
			IF(
			    [Profit Margin] >= [Profit Margin Target],
			    "Target Achieved",
			    "Below Expectation"
			)
		lineageTag: 9cb2ad05-2c1a-4df9-b3d4-8d8d494b8438

	measure '_Refere PrevMonth' =
			
			--"vs prev. month"
			VAR PrevMonth =
			    CALCULATE(
			        MIN('Calendar'[Month Year]),
			        DATEADD(
			            'Calendar'[Date],
			            -1,
			            MONTH
			        )
			    )
			VAR Label =
			    IF(
			        ISBLANK(PrevMonth),
			        "No prev. months available",
			        "vs " & PrevMonth
			    )
			RETURN Label
		lineageTag: 1d76d516-a260-4bbd-a5d0-28414436868c

	measure 'ROI PM' = ```
			
			VAR Delta = [ROI] - [ROI Target]
			VAR FormatDelta = FORMAT([ROI] - [ROI Target], "+0.00;-0.00") 
			VAR Label =
			    IF(
			        Delta >= 0, " over expected",
			        " below expected"
			    )
			RETURN FormatDelta & Label
			```
		displayFolder: ROI
		lineageTag: a1414c65-d416-4c86-9033-1277468d1a6c

	measure 'ROAS PM' = ```
			
			VAR Delta = [ROAS] - [ROAS Target]
			VAR FormatDelta = FORMAT([ROAS] - [ROAS Target], "+0.00;-0.00") 
			VAR Label =
			    IF(
			        Delta >= 0, " over expected",
			        " below expected"
			    )
			RETURN FormatDelta & Label
			```
		displayFolder: ROAS
		lineageTag: 38d78ccb-d839-4d73-b6d1-7a3351437b4a

	measure 'Total Impressions' = SUM(Metrics[Impressions])
		formatString: #,0
		displayFolder: Impressions
		lineageTag: 8e8ad667-4a31-4acc-a7de-b3a83cd29505

	measure 'PM 2' = ```
			
			IF(
			    [Profit Margin] > [Profit Margin Target], 
			    0,
			    [Profit Margin Target] - [Profit Margin]
			)
			```
		formatString: 0.0000
		displayFolder: Profit
		lineageTag: 8e9f3fe4-b7c7-4c69-a364-2a79bc3882cb

	measure 'Total Clicks' = SUM(Metrics[Clicks])
		formatString: #,0
		displayFolder: Clicks
		lineageTag: 3baf4e5f-2e36-477b-a3b1-2d1204e70bd3

	measure Click-Through-Rate =
			
			DIVIDE(
			    [Total Clicks],
			    [Total Impressions]
			)
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: Clicks
		lineageTag: 7da4c0fc-073a-48be-83cf-c0f4caebe659

	measure 'Vs Prev. Month - CTR' =
			
			VAR PrevMonth =
			    CALCULATE(
			        [Click-Through-Rate],
			        DATEADD(
			            'Calendar'[Date],
			            -1,
			            MONTH
			        )
			    )
			VAR CheckBlankMonth =
			    IF(
			        ISBLANK(PrevMonth),
			        " ",
			        VAR Delta = ROUND(100*([Click-Through-Rate] - PrevMonth), 2)
			        VAR Sign_ =
			            IF(
			                Delta > 0,
			                "â–² +",
			                "â–¼ "
			            )
			        RETURN Sign_ & Delta
			    )
			RETURN CheckBlankMonth
		displayFolder: Clicks
		lineageTag: 69f9905c-fadc-4506-bfe3-40a48ca6dc42

	measure 'Total Conversion' = SUM(Metrics[Conversions])
		formatString: #,0
		displayFolder: Conversions
		lineageTag: 4342b930-c560-4b7b-ada7-bde6b566709d

	measure 'Conversion Rate' =
			
			DIVIDE(
			    [Total Conversion],
			    [Total Clicks]
			)
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: Conversions
		lineageTag: 9655aeaf-4da3-4c7e-9e18-2235e9f59686

	measure 'Total Impressions % Change' =
			
			VAR PrevMonth =
			    CALCULATE(
			        [Total Impressions],
			        DATEADD(
			            'Calendar'[Date],
			            -1,
			            MONTH
			        )
			    )
			VAR PctChange =
			    DIVIDE(
			        [Total Impressions],
			        PrevMonth
			    ) - 1
			RETURN PctChange
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: Impressions
		lineageTag: e78b1d93-8992-4401-b4c4-b78b0b9b7878

	measure 'Tot. Impressions Format' = CONVERT(FORMAT([Total Impressions], "#,##"), STRING)
		displayFolder: Impressions
		lineageTag: e728e0ca-d59b-4a8b-ac70-7f8054fddb69

	measure AddLabel = ```
			"vs Previous Month" 
			```
		displayFolder: Impressions
		lineageTag: d2aab9d4-fca4-464e-b59a-3c2938a0a2d8

	measure 'Format Color CTR' =
			
			VAR PrevMonth =
			    CALCULATE(
			        [Click-Through-Rate],
			        DATEADD(
			            'Calendar'[Date],
			            -1,
			            MONTH
			        )
			    )
			VAR SwitchColor =
			    IF(
			        [Click-Through-Rate] > PrevMonth,
			        1,
			        0
			    )
			RETURN SwitchColor
		formatString: 0
		displayFolder: Clicks
		lineageTag: ec78d37d-3cb8-499b-ab2d-c1ab47b93d90

	measure _Blank = BLANK()
		formatString: 0
		lineageTag: 5dfb65e5-a541-441c-8da3-bbc7ad9c803d

	measure 'Total Clicks Label' =
			
			VAR FormatMetric =
			    FORMAT(
			        [Total Clicks],
			        "#,##"
			    )
			VAR Label = "Clicks " & FormatMetric
			RETURN Label
		displayFolder: Clicks
		lineageTag: 05755e97-a5e1-49a5-9b23-219dd115ff73

	measure 'Total Impressions Label' =
			
			VAR FormatMetric =
			    FORMAT(
			        [Total Impressions],
			        "#,##"
			    )
			VAR Label = "Impressions " & FormatMetric
			RETURN Label
		displayFolder: Impressions
		lineageTag: a953ca70-bedc-4c34-b9fe-ec259efb8980

	measure 'Impression-to-Conversion Rate' =
			
			DIVIDE(
			    [Total Conversion],
			    [Total Impressions]
			)
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: Conversions
		lineageTag: 2bb1a646-2e4e-4f86-8658-7050e587cc29

	measure 'Vs Prev. Month - ICR' =
			
			VAR PrevMonth =
			    CALCULATE(
			        [Impression-to-Conversion Rate],
			        DATEADD(
			            'Calendar'[Date],
			            -1,
			            MONTH
			        )
			    )
			VAR CheckBlankMonth =
			    IF(
			        ISBLANK(PrevMonth),
			        " ",
			    VAR Delta = ROUND(100*([Impression-to-Conversion Rate] - PrevMonth), 2)
			    VAR Sign_ =
			        IF(
			            Delta > 0,
			            "â–² +",
			            "â–¼ "
			        )
			    RETURN Sign_ & Delta
			    )
			RETURN CheckBlankMonth
		displayFolder: Conversions
		lineageTag: 8ffed6ed-89ee-40bc-8927-d485bb6bbdbc

	measure 'Format Color ICR' =
			
			VAR PrevMonth =
			    CALCULATE(
			        [Impression-to-Conversion Rate],
			        DATEADD(
			            'Calendar'[Date],
			            -1,
			            MONTH
			        )
			    )
			VAR SwitchColor =
			    IF(
			        [Impression-to-Conversion Rate] > PrevMonth,
			        1,
			        0
			    )
			RETURN SwitchColor
		formatString: 0
		displayFolder: Conversions
		lineageTag: 91946b90-191f-4dcc-8b98-b7910fca1fef

	measure 'Vs Prev. Month - CR' =
			
			VAR PrevMonth =
			    CALCULATE(
			        [Conversion Rate],
			        DATEADD(
			            'Calendar'[Date],
			            -1,
			            MONTH
			        )
			    )
			VAR CheckBlankMonth =
			    IF(
			        ISBLANK(PrevMonth),
			        " ",
			    VAR Delta = ROUND(100*([Conversion Rate] - PrevMonth), 2)
			    VAR Sign_ =
			        IF(
			            Delta > 0,
			            "â–² +",
			            "â–¼ "
			        )
			    RETURN Sign_ & Delta
			    )
			RETURN CheckBlankMonth
		displayFolder: Conversions
		lineageTag: a453bdf8-ceeb-4028-a332-2f8493e4a9bb

	measure 'Format Color CR' =
			
			VAR PrevMonth =
			    CALCULATE(
			        [Conversion Rate],
			        DATEADD(
			            'Calendar'[Date],
			            -1,
			            MONTH
			        )
			    )
			VAR SwitchColor =
			    IF(
			        [Conversion Rate] > PrevMonth,
			        1,
			        0
			    )
			RETURN SwitchColor
		formatString: 0
		displayFolder: Conversions
		lineageTag: 64a411a0-ada5-46d4-9b37-ca7bf7c0c594

	measure 'Total Cost-per-Click' =
			
			VAR TotalCost = SUM(Metrics[Cost])
			VAR Result =
			    DIVIDE(
			        TotalCost,
			        [Total Clicks]
			    )
			RETURN Result
		formatString: #,0.00\ "â‚¬";-#,0.00\ "â‚¬";#,0.00\ "â‚¬"
		displayFolder: Clicks
		lineageTag: 33cecd1d-73f7-44e6-a602-ae1454917fbc

	measure 'Total CPConversion' =
			
			VAR TotalCost = SUM(Metrics[Cost])
			VAR Result =
			    DIVIDE(
			        TotalCost,
			        [Total Conversion]
			    )
			RETURN Result
		formatString: #,0.00\ "â‚¬";-#,0.00\ "â‚¬";#,0.00\ "â‚¬"
		displayFolder: Conversions
		lineageTag: 149e16d1-843d-44d5-b487-f61fb39e5291

	measure 'Total eCPM' =
			
			VAR TotalCost = SUM(Metrics[Cost])
			VAR Result =
			    100*DIVIDE(
			            TotalCost,
			            [Total Impressions]
			        )
			RETURN Result
		formatString: #,0.00\ "â‚¬";-#,0.00\ "â‚¬";#,0.00\ "â‚¬"
		displayFolder: Impressions
		lineageTag: 4c315843-cf79-4e4b-8cce-b28bea530d84

	measure 'Total RPC' =
			
			DIVIDE(
			    [Revenue],
			    [Total CPConversion]
			)
		formatString: #,0.00\ "â‚¬";-#,0.00\ "â‚¬";#,0.00\ "â‚¬"
		displayFolder: Conversions
		lineageTag: d6c0dca4-853a-45b6-a68c-6cff595e91a5

	measure 'Top Campaign' =
			
			VAR Rank_ =
			    TOPN(
			        1,
			        VALUES(Campaign[Campaign]),
			        [Total Clicks],
			        DESC
			    )
			// VAR SelectTop =
			//     MIN(
			//         Campaign[Campaign],
			//         Rank_ = 1
			//     )
			RETURN Rank_
		displayFolder: Clicks
		lineageTag: e3ab889e-ab37-4082-9c5f-517739e7e56e

	measure '_Refere Month' = "in " & SELECTEDVALUE('Calendar'[Month Year])
		lineageTag: 18193456-b281-4f46-ad68-a17f46a3f073

	measure 'Total RPM' =
			
			VAR Result =
			    100*DIVIDE(
			            [Revenue],
			            [Total Impressions]
			        )
			RETURN Result
		formatString: #,0.00\ "â‚¬";-#,0.00\ "â‚¬";#,0.00\ "â‚¬"
		displayFolder: Impressions
		lineageTag: cd717d83-90fa-4908-bf98-a288edd6ae64

	measure 'Total Revenue-per-Click' =
			
			VAR Result =
			    DIVIDE(
			        [Revenue],
			        [Total Clicks]
			    )
			RETURN Result
		formatString: #,0.00\ "â‚¬";-#,0.00\ "â‚¬";#,0.00\ "â‚¬"
		displayFolder: Clicks
		lineageTag: 696fc683-1dfa-41dc-afcb-588ab82acdd8

	measure Zero = 0
		formatString: 0
		lineageTag: 1a3d7b2f-4826-4d1d-a412-a22df53072f3

	measure 'CTR Delta Chart' =
			
			VAR PrevMonth =
			    CALCULATE(
			        [Click-Through-Rate],
			        DATEADD(
			            'Calendar'[Date],
			            -1,
			            MONTH
			        )
			    )
			VAR Delta = 100*([Click-Through-Rate] - PrevMonth)
			RETURN Delta
		formatString: 0.00
		displayFolder: Clicks
		lineageTag: 281ce63f-dce5-49b7-9139-430b4c09c1e5

	measure 'ProfM Comparison' =
			
			[Profit] - [Profit Margin Target]
		displayFolder: Profit
		lineageTag: e74b4e68-604a-40c9-939e-8c2de7fcbf66

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'ROAS Comparison' =
			
			[ROAS] - [ROAS Target]
		displayFolder: ROAS
		lineageTag: b92b41c0-29e1-4754-87f9-05c687485e4a

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'ROI Comparison' =
			
			[ROI] - [ROI Target]
		displayFolder: ROI
		lineageTag: 15d6f8bf-d8c0-4aeb-ad7b-b2da5ab66dcb

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Total Impressions PM' =
			
			CALCULATE(
			    [Total Impressions],
			    DATEADD(
			        'Calendar'[Date],
			        -1,
			        MONTH
			    )
			)
		formatString: #,0
		displayFolder: Impressions
		lineageTag: 5f871ba2-9663-45d8-8e84-86cf6c368e76

	measure 'Format Color Impression' =
			
			VAR AverageImpressions =
			CALCULATE(
			    AVERAGEX(
			        ALL('Calendar'[Month Year], 'Calendar'[Month Year S]),
			        [Total Impressions]
			    ),
			    ALL('Calendar'[Month Year])
			)
			VAR CheckAgainstAverage =
			    IF(
			        [Total Impressions] >= AverageImpressions,
			        1,
			        0
			    )
			RETURN CheckAgainstAverage
		displayFolder: Impressions
		lineageTag: 649691d7-206c-47a2-a432-50ae1e5284c8

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Dynamic Format Column Color' = ```
			
			VAR SelectedMeasure = MIN('Decomposition Metric'[Decomposition Metric])
			VAR IsAboveAverage =
			    SWITCH(
			        SelectedMeasure,
			        "Impressions", 
			            VAR AvgImpressions = 
			                CALCULATE(
			                    AVERAGEX(
			                        ALL('Calendar'[Month Year], 'Calendar'[Month Year S]),
			                        [Total Impressions]
			                    ),
			                    ALL('Calendar'[Month Year])
			                )
			            VAR Impressions_vs_Average =
			                IF(
			                    [Total Impressions] >= AvgImpressions,
			                    1,
			                    0
			                )
			            RETURN Impressions_vs_Average,
			        "Clicks",
			            VAR AvgClicks =
			                CALCULATE(
			                    AVERAGEX(
			                        ALL('Calendar'[Month Year], 'Calendar'[Month Year S]),
			                        [Total Clicks]
			                    ),
			                    ALL('Calendar'[Month Year])
			                )
			            VAR Clicks_vs_Average =
			                IF(
			                    [Total Clicks] >= AvgClicks,
			                    1,
			                    0
			                )
			            RETURN Clicks_vs_Average,
			        "Conversions", 
			            VAR AvgConversions =
			                CALCULATE(
			                    AVERAGEX(
			                        ALL('Calendar'[Month Year], 'Calendar'[Month Year S]),
			                        [Total Conversion]
			                    ),
			                    ALL('Calendar'[Month Year])
			                )
			            VAR Conversions_vs_Average =
			                IF(
			                    [Total Conversion] >= AvgConversions,
			                    1,
			                    0
			                )
			            RETURN Conversions_vs_Average
			    )
			RETURN IsAboveAverage
			```
		lineageTag: 87ad2abc-bd81-4fac-b5ac-53da7d539985

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Dynamic Column Tooltip PM' = ```
			
			VAR FirstMonthAvailable =
			    CALCULATE(
			        MIN('Calendar'[Month Year S]),
			        ALL('Calendar'[Month Year], 'Calendar'[Month Year S])
			    )
			VAR MonthInColumn = MIN('Calendar'[Month Year S])
			VAR SelectedMeasure = MIN('Decomposition Metric'[Decomposition Metric])
			VAR PCtChange =
			    SWITCH(
			        TRUE(),
			        SelectedMeasure = "Impressions", 
			            VAR ImpressionPM =    
			                CALCULATE(
			                    [Total Impressions],
			                    PREVIOUSMONTH('Calendar'[Date])
			                )
			            RETURN
			                DIVIDE(
			                    [Total Impressions],
			                    ImpressionPM
			                ) - 1,
			        SelectedMeasure = "Clicks",
			        VAR ClicksPM =
			            CALCULATE(
			                [Total Clicks],
			                PREVIOUSMONTH('Calendar'[Date])
			            )
			        RETURN
			            DIVIDE(
			                [Total Clicks],
			                ClicksPM
			            ) - 1,
			        SelectedMeasure = "Conversions",
			        VAR ConversionsPM =
			            CALCULATE(
			                [Total Conversion],
			                PREVIOUSMONTH('Calendar'[Date])
			            )
			        RETURN
			            DIVIDE(
			                [Total Conversion],
			                ConversionsPM,
			                BLANK()
			            ) - 1
			    )
			VAR FormatPctChange =
			    FORMAT(
			        PCtChange,
			        "Percent"
			    )
			VAR FormatSign =
			    SWITCH(
			        TRUE(),
			        MonthInColumn =  FirstMonthAvailable, BLANK(),
			        PCtChange > 0, "â–² +" & FormatPctChange,
			        "â–¼ " & FormatPctChange
			    )
			RETURN FormatSign
			```
		lineageTag: 57a6dc24-e605-400a-8afc-e0edbe44d4d7

	measure _NoPM =
			
			VAR FirstMonthAvailable =
			    CALCULATE(
			        MIN('Calendar'[Month Year S]),
			        ALL('Calendar'[Month Year], 'Calendar'[Month Year S])
			    )
			VAR MonthInChart = MIN('Calendar'[Month Year S])
			VAR CheckExistencePM =
			    IF(
			        MonthInChart =  FirstMonthAvailable,
			        BLANK(),
			        1
			    )
			RETURN CheckExistencePM
		formatString: 0
		lineageTag: eaab6756-7902-42da-b9d2-a9a2dd9bbab6

	measure 'Format Total Clicks' = ```
			
			ROUND(
			    [Total Clicks]/1000000,
			    2) & "M"
			
			```
		displayFolder: Clicks
		lineageTag: b24f0f22-7f56-4199-82ff-160e3d709f47

	measure 'Format Total Impressions' =
			
			ROUND(
			    [Total Impressions]/1000000,
			    2) & "M"
		displayFolder: Impressions
		lineageTag: 27283824-93d9-4dd6-812a-24cfb5e3a514

	measure 'Format Total Conversions' =
			
			ROUND(
			    [Total Conversion]/1000000,
			    2) & "M"
		displayFolder: Conversions
		lineageTag: 98fc55e0-2670-4266-95a1-e534dffb6710

	partition Measure = m
		mode: import
		source =
				let
				    Source = Table.FromRows(Json.Document(Binary.Decompress(Binary.FromText("i44FAA==", BinaryEncoding.Base64), Compression.Deflate)), let _t = ((type nullable text) meta [Serialized.Text = true]) in type table [Column1 = _t]),
				    #"Changed Type" = Table.TransformColumnTypes(Source,{{"Column1", type text}}),
				    #"Removed Columns" = Table.RemoveColumns(#"Changed Type",{"Column1"})
				in
				    #"Removed Columns"

	annotation PBI_ResultType = Table

	annotation PBI_NavigationStepName = Navigation

